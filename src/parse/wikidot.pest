//
// wikidot.pest
//
// wikidot-html - Convert Wikidot code to HTML
// Copyright (C) 2019 Ammon Smith for Project Foundation
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
// GNU Affero General Public License for more details.
//
// You should have received a copy of the GNU Affero General Public License
// along with this program. If not, see <http://www.gnu.org/licenses/>.
//

page = { SOI ~ (line ~ newlines)* ~ EOI }
newlines = { "\n"+ | EOI }

// TODO finish line declaration
line = {
    clear_float |
    footnote_block |
    horizontal_line |
    module |
    note |
    word+
}

// Misc lines
clear_float = { "~"{4,} ~ direction? }
footnote_block = { "[[" ~ space? ~ ^"footnoteblock" ~ space? ~ "]]" }
horizontal_line = { "-"{4,} }

// Block lines
module = {
    "[[" ~ space? ~ ^"module" ~ space ~ ident ~ module_arg* ~ space? ~ "]]" ~
    (line* ~ "[[/" ~ space? ~ ^"module" ~ space? ~ "]]")?
}
module_arg = { space ~ ident ~ space? ~ "=" ~ space? ~ string }
note = {
    "[[" ~ space? ~ ^"note" ~ space? ~ "]]" ~
    line* ~
    "[[/" ~ space? ~ ^"note" ~ space? ~ "]]"
}

// Word definition
word = {
    text |
    raw |
    legacy_raw |
    email |
    italics |
    strikethrough |
    bold |
    underline |
    subscript |
    superscript |
    monospace |
    anchor |
    date |
    equation_ref |
    file_ref |
    footnote |
    image |
    span |
    user
}

// Misc words
text = { (!token ~ ANY)+ }
raw = { ("@@" ~ (!"@@" ~ ANY)* ~ "@@") | "@@@@@@" }
legacy_raw = { ("``" ~ (!"``" ~ ANY)+ ~ "``") | "``````" }
email = { ident ~ "@" ~ ident }

// Formatting words
color = { "##" ~ (("#" ~ hex_digit+) | ident) ~ "|" ~ word* ~ "##" }
italics = { "//" ~ word* ~ "//" }
strikethrough = { "--" ~ word* ~ "--" }
bold = { "**" ~ word* ~ "**" }
underline = { "__" ~ word* ~ "__" }
subscript = { ",," ~ word* ~ ",," }
superscript = { "^^" ~ word* ~ "^^" }
monospace = { "{{" ~ word* ~ "}}" }

// Block words
anchor = { "[[#" ~ space? ~ ident ~ space? ~ "]]" }
date = { "[[" ~ space? ~ ^"date" ~ space ~ integer ~ (^"format" ~ "=" ~ string)? ~ space? ~ "]]" }
equation_ref = { "[[" ~ space? ~ ^"eref" ~ space ~ ident ~ space? ~ "]]" }
file_ref = { "[[" ~ space? ~ ^"file" ~ space ~ ident ~ space? ~ "]]" }
footnote = {
    "[[" ~ space? ~ ^"footnote" ~ space? ~ "]]" ~
    word* ~
    "[[/" ~ space? ~ ^"footnote" ~ space? ~ "]]"
}
image = {
    "[[" ~ space? ~ ^"image" ~ space ~ ident ~ image_arg* ~ space? ~ "]]"
}
image_arg = {
    space ~
    (^"link" | ^"alt" | ^"title" | ^"width" | ^"height" | ^"style" | ^"class" | ^"size") ~
    space? ~ "=" ~ space? ~ string
}
span = {
    "[[" ~ space? ~ ^"span" ~ span_arg* ~ space? ~ "]]" ~
    word* ~
    "[[/" ~ ^"span" ~ space? ~ "]]"
}
span_arg = { space ~ (^"id" | ^"class" | ^"style") ~ space? ~ "=" ~ space? ~ string }
user = { "[[" ~ "*"? ~ space? ~ ^"user" ~ space ~ ident ~ space? ~ "]]" }

// Components
token = @{
    "@@" |
    "``" |
    "//" |
    "--" |
    "**" |
    "__" |
    ",," |
    "^^" |
    "{{" | "}}" |
    "[[" | "]]" |
    "-"{4,} | "~"{4,}
}
hex_digit = @{ '0'..'9' | 'a'..'f' | 'A'..'F' }
space = _{ (" " | "\t")+ }
integer = @{ "-"? ~ ASCII_DIGIT+ }
ident = @{ (ASCII_ALPHANUMERIC | "-" | "+" | "_" | "." | "%")+ }
inner_str = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ inner_str)? }
inner_chr = @{ escape | ANY }
string = @{ "\"" ~ inner_str ~ "\"" }
escape = @{ "\\" ~ ("\"" | "\\" | "r" | "n" | "t" | "0" | "'") }
direction = { "<" | ">" | "=" | "==" }
COMMENT = _{ "[!--" ~ (!"--]" ~ ANY)* ~ "--]" }
