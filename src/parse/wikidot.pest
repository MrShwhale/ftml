//
// wikidot.pest
//
// wikidot-html - Convert Wikidot code to HTML
// Copyright (C) 2019 Ammon Smith for Project Foundation
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
// GNU Affero General Public License for more details.
//
// You should have received a copy of the GNU Affero General Public License
// along with this program. If not, see <http://www.gnu.org/licenses/>.
//

page = { SOI ~ line* ~ EOI }
newlines = { "\n"+ | EOI }

// TODO finish line declaration
line = { line_inner ~ newlines }
line_inner = {
    clear_float |
    horizontal_line |
    center |
    word+
}

// Misc lines
clear_float = { "~"{4,} ~ direction? }
horizontal_line = { "-"{4,} }

// Formatting lines
center = { "=" ~ word+ }

// Word definition
word = {
    text |
    raw |
    legacy_raw |
    email |
    color |
    italics |
    strikethrough |
    bold |
    underline |
    subscript |
    superscript |
    monospace |
    anchor |
    date |
    equation_ref |
    file_ref |
    footnote |
    footnote_block |
    form |
    module |
    note |
    image |
    span |
    user
}

// Misc words
text = {
    (!(
        "@@" |
        "``" |
        "//" |
        "--" |
        "**" |
        "__" |
        ",," |
        "^^" |
        "{{" | "}}" |
        "[[" | "]]" |
        "##" |
        "-"{4,} | "~"{4,} |
        "\n"
    ) ~ ANY)+
}
raw = { ("@@" ~ (!("@@" | "\n") ~ ANY)+ ~ "@@") | "@@@@@@" }
legacy_raw = { ("``" ~ (!("``" | "\n") ~ ANY)+ ~ "``") | "``````" }
email = { ident ~ "@" ~ ident }

// Formatting words
color = { "##" ~ space? ~ ("#"? ~ ident) ~ space? ~ "|" ~ word* ~ "##" }
italics = { "//" ~ word* ~ "//" }
strikethrough = { "--" ~ word+ ~ "--" }
bold = { "**" ~ word* ~ "**" }
underline = { "__" ~ word* ~ "__" }
subscript = { ",," ~ word* ~ ",," }
superscript = { "^^" ~ word* ~ "^^" }
monospace = { "{{" ~ word* ~ "}}" }

// Block words
anchor = { "[[#" ~ space? ~ ident ~ space? ~ "]]" }
date = { "[[" ~ space? ~ ^"date" ~ space ~ integer ~ (^"format" ~ "=" ~ string)? ~ space? ~ "]]" }
div = {
    "[[" ~ space? ~ ^"div" ~ div_arg* ~ space? ~ "]]" ~
    line* ~
    "[[/" ~ space? ~ ^"div" ~ space? ~ "]]"
}
div_arg = { space ~ (^"id" | ^"class" | ^"style") ~ space? ~ "=" ~ space? ~ string }
equation_ref = { "[[" ~ space? ~ ^"eref" ~ space ~ ident ~ space? ~ "]]" }
file_ref = { "[[" ~ space? ~ ^"file" ~ space ~ file_ident ~ space? ~ "]]" }
footnote = {
    "[[" ~ space? ~ ^"footnote" ~ space? ~ "]]" ~
    word* ~
    "[[/" ~ space? ~ ^"footnote" ~ space? ~ "]]"
}
footnote_block = { "[[" ~ space? ~ ^"footnoteblock" ~ space? ~ "]]" }
form = {
    "[[" ~ space? ~ ^"form" ~ space? ~ "]]\n" ~
    (!("[[/" ~ space? ~ ^"form") ~ ANY)* ~
    "[[/" ~ space? ~ ^"form" ~ space? ~ "]]"
}
module = {
    "[[" ~ space? ~ ^"module" ~ space ~ ident ~ module_arg* ~ space? ~ "]]" ~
    (
        "\n" ~ (!("[[/" ~ space? ~ ^"module") ~ ANY)* ~
        "[[/" ~ space? ~ ^"module" ~ space? ~ "]]"
    )?
}
module_arg = { space ~ ident ~ space? ~ "=" ~ space? ~ string }
note = {
    "[[" ~ space? ~ ^"note" ~ space? ~ "]]\n" ~
    line* ~
    "[[/" ~ space? ~ ^"note" ~ space? ~ "]]"
}
image = {
    "[[" ~ space? ~ ^"image" ~ space ~ file_ident ~ image_arg* ~ space? ~ "]]"
}
image_arg = {
    space ~
    (^"link" | ^"alt" | ^"title" | ^"width" | ^"height" | ^"style" | ^"class" | ^"size") ~
    space? ~ "=" ~ space? ~ string
}
span = {
    "[[" ~ space? ~ ^"span" ~ span_arg* ~ space? ~ "]]" ~
    word* ~
    "[[/" ~ space? ~ ^"span" ~ space? ~ "]]"
}
span_arg = { space ~ (^"id" | ^"class" | ^"style") ~ space? ~ "=" ~ space? ~ string }
user = { "[[" ~ space? ~ "*"? ~ space? ~ ^"user" ~ space ~ ident ~ space? ~ "]]" }

// Components
hex_digit = @{ '0'..'9' | 'a'..'f' | 'A'..'F' }
space = _{ (" " | "\t")+ }
integer = @{ "-"? ~ ASCII_DIGIT+ }
file_ident = @{ (!(space | "]]") ~ ANY)+ }
ident = @{ (ASCII_ALPHANUMERIC | "-" | "+" | "_" | "." | "%")+ }
str_inner = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ str_inner)? }
string = @{ "\"" ~ str_inner ~ "\"" }
escape = @{ "\\" ~ ("\"" | "\\" | "r" | "n" | "t" | "0" | "'") }
direction = { "<" | ">" | "="{1,2} }
COMMENT = _{ "[!--" ~ (!"--]" ~ ANY)* ~ "--]" }
