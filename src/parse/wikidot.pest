//
// parse/wikidot.pest
//
// wikidot-html - Convert Wikidot code to HTML
// Copyright (C) 2019 Ammon Smith for Project Foundation
//
// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
// GNU Affero General Public License for more details.
//
// You should have received a copy of the GNU Affero General Public License
// along with this program. If not, see <http://www.gnu.org/licenses/>.
//

// Pages {{{
page = { SOI ~ line* ~ EOI }
// }}}

// TODO
// Lines {{{
line = { line_inner ~ ("\n" | EOI) | just_newlines }
lines_internal = { space? ~ line* ~ line_inner? ~ ("\n" | EOI)? }

line_inner = {
    align |
    code |
    javascript |
    clear_float |
    div |
    bullet_list |
    numbered_list |
    horizontal_line |
    quote_block |
    words
}

just_newlines = { "\n"+ }
// }}}

// Block lines {{{
align = {
    ("[[>]]\n" ~ line* ~ "[[/>]]") |
    ("[[<]]\n" ~ line* ~ "[[/<]]") |
    ("[[=]]\n" ~ line* ~ "[[/=]]") |
    ("[[==]]\n" ~ line* ~ "[[/==]]")
}

code = {
    "[[" ~ space? ~ ^"code" ~ code_arg? ~ space? ~ "]]\n" ~
    (!("[[/" ~ space? ~ ^"code") ~ ANY)* ~
    "[[/" ~ space? ~ ^"code" ~ space? ~ "]]"
}
code_arg = { space ~ (^"type" | ^"language" | ^"lang") ~ space? ~ "=" ~ space? ~ string }

div = {
    "[[" ~ space? ~ ^"div" ~ "_"? ~ div_arg* ~ space? ~ "]]\n" ~
    line* ~
    "[[/" ~ space? ~ ^"div" ~ "_"? ~ space? ~ "]]"
}
div_arg = { space ~ (^"id" | ^"class" | ^"style") ~ space? ~ "=" ~ space? ~ string }

javascript = {
    "[[" ~ space? ~ (^"js" | ^"javascript") ~ space? ~ "]]\n" ~
    (!("[[/" ~ space? ~ (^"js" | ^"javascript")) ~ ANY)* ~
    "[[/" ~ space? ~ (^"js" | ^"javascript") ~ space? ~ "]]"
}

quote_block = {
    "[[" ~ space? ~ (^"quote" | ^"blockquote") ~ quote_block_arg* ~ space? ~ "]]\n" ~
    lines_internal ~
    "[[/" ~ space? ~ (^"quote" | ^"blockquote") ~ space? ~ "]]"
}
quote_block_arg = { space ~ (^"id" | ^"class" | ^"style") ~ space? ~ "=" ~ space? ~ string }
// }}}

// Misc lines {{{
bullet_list = { (" "* ~ "* " ~ list_item)+ }
numbered_list = { (" "* ~ "# " ~ list_item)+ }
list_item = { word+ }
words = {
    (
        "+"{1,6} ~ (" " | "\t") |
        (!"+" ~ "="?)
    ) ~ word+
}
clear_float = { "~"{4,} ~ ("<" | "=" | ">")? }
horizontal_line = { "-"{4,} }
// }}}

// Words {{{
word = {
    text |
    raw |
    legacy_raw |
    email |
    color |
    italics |
    strikethrough |
    bold |
    underline |
    subscript |
    superscript |
    monospace |
    anchor |
    anchor_tag |
    date |
    equation_ref |
    file_ref |
    footnote |
    footnote_block |
    form |
    gallery |
    module |
    note |
    image |
    size |
    span |
    tab_list |
    user |
    link_bare |
    link_page |
    link_url
}
// }}}

// Formatting words {{{
color = { "##" ~ space? ~ ("#"? ~ ident) ~ space? ~ "|" ~ word* ~ "##" }
italics = { "//" ~ word* ~ "//" }
strikethrough = { "--" ~ word+ ~ "--" }
bold = { "**" ~ word* ~ "**" }
underline = { "__" ~ word* ~ "__" }
subscript = { ",," ~ word* ~ ",," }
superscript = { "^^" ~ word* ~ "^^" }
monospace = { "{{" ~ word* ~ "}}" }
// }}}

// Block words {{{
anchor = { "[[#" ~ space? ~ ident ~ space? ~ "]]" }

date = {
    "[[" ~ space? ~ ^"date" ~ space ~ integer ~ date_arg? ~ space? ~ "]]"
}
date_arg = @{ space ~ ^"format" ~ space? ~ "=" ~ space? ~ string }

equation_ref = {
    "[[" ~ space? ~ (^"eref" | ^"equationref") ~ space ~ ident ~ space? ~ "]]"
}

file_ref = { "[[" ~ space? ~ ^"file" ~ space ~ file_ident ~ space? ~ "]]" }

footnote = {
    "[[" ~ space? ~ ^"footnote" ~ space? ~ "]]" ~
    lines_internal ~
    "[[/" ~ space? ~ ^"footnote" ~ space? ~ "]]"
}

footnote_block = { "[[" ~ space? ~ ^"footnoteblock" ~ space? ~ "]]" }

form = {
    "[[" ~ space? ~ ^"form" ~ space? ~ "]]\n" ~
    (!("[[/" ~ space? ~ ^"form") ~ ANY)* ~
    "[[/" ~ space? ~ ^"form" ~ space? ~ "]]"
}

gallery = { "[[" ~ space? ~ ^"gallery" ~ space? ~ "]]" }

module = {
    "[[" ~ space? ~ ^"module" ~ space ~ ident ~ module_arg* ~ space? ~ "]]" ~
    (
        "\n" ~ (!("[[/" ~ space? ~ ^"module") ~ ANY)* ~
        "[[/" ~ space? ~ ^"module" ~ space? ~ "]]"
    )?
}
module_arg = { space ~ ident ~ space? ~ "=" ~ space? ~ string }

note = {
    "[[" ~ space? ~ ^"note" ~ space? ~ "]]\n" ~
    line* ~
    "[[/" ~ space? ~ ^"note" ~ space? ~ "]]"
}

image = {
    "[[" ~ space? ~ image_alignment? ~ space? ~ ^"image" ~ space ~ file_ident ~ image_arg* ~ space? ~ "]]"
}
image_alignment = {
    space? ~ ("f"? ~ ("<" | ">") | "=")?
}
image_arg = {
    space ~
    (^"link" | ^"alt" | ^"title" | ^"width" | ^"height" | ^"style" | ^"class" | ^"size") ~
    space? ~ "=" ~ space? ~ string
}

size = {
    "[[" ~ space? ~ ^"size" ~ space ~ size_arg ~ space? ~ "]]" ~
    lines_internal ~
    "[[/" ~ space? ~ ^"size" ~ space? ~ "]]"
}
size_arg = {
    (ASCII_DIGIT | "."){1,5} ~ space? ~ (^"rem" | ^"em" | ^"px" | "%") |
    ^"xx-small" |
    ^"x-small" |
    ^"smaller" |
    ^"larger" |
    ^"small" |
    ^"medium" |
    ^"large" |
    ^"x-large" |
    ^"xx-large"
}

span = {
    "[[" ~ space? ~ ^"span" ~ span_arg* ~ space? ~ "]]" ~
    lines_internal ~
    "[[/" ~ space? ~ ^"span" ~ space? ~ "]]"
}
span_arg = { space ~ (^"id" | ^"class" | ^"style") ~ space? ~ "=" ~ space? ~ string }

tab_list = {
    "[[" ~ space? ~ (^"tablist" | ^"tabview" | ^"tabs") ~ space? ~ "]]" ~ space? ~
    (tab ~ space?)* ~
    "[[/" ~ space? ~ (^"tablist" | ^"tabview" | ^"tabs") ~ space? ~ "]]"
}
tab = {
    "[[" ~ space? ~ ^"tab" ~ space ~ tab_name ~ space? ~ "]]" ~ space? ~
    lines_internal ~
    "[[/" ~ space? ~ ^"tab" ~ space? ~ "]]"
}
tab_name = { (!("]]" | "\n") ~ ANY)+ }

user = { "[[" ~ space? ~ "*"? ~ space? ~ ^"user" ~ space ~ ident ~ space? ~ "]]" }
// }}}

// Link words {{{
anchor_tag = {
    "[[" ~ space? ~ ^"a" ~ "_"? ~ anchor_arg* ~ space? ~ "]]" ~
    word* ~
    "[[/" ~ space? ~ ^"a" ~ "_"? ~ space? ~ "]]"
}
anchor_arg = {
    space ~ (^"href" | ^"name" | ^"id" | ^"class" | ^"style" | ^"target") ~ space? ~
    "=" ~ space? ~ string
}

link_bare = { link_newtab ~ url }

link_page = {
    "[[[" ~ space? ~ link_newtab ~ space? ~ link_page_href ~
    (space? ~ "|" ~ space? ~ link_page_name)? ~ space? ~ "]]]"
}
link_page_href = { (!("\n" | "|" | "]]]") ~ ANY)+ }
link_page_name = { (!("\n" | "[" | "]]]") ~ ANY)+ }

link_url = {
    "[" ~ !"[" ~ space? ~ link_newtab ~ space? ~
     link_url_href ~ space ~
     link_url_name ~ space? ~ "]"
}
link_url_href = { ^"mailto:"? ~ email | root_url | url }
link_url_name = { (!("\n" | "[" | "]") ~ ANY)+ }

link_newtab = { "*"? }
// }}}

// Misc words {{{
text = {
    (!(
        "@@" |
        "``" |
        "//" |
        "--" |
        "**" |
        "__" |
        ",," |
        "^^" |
        ("[" ~ space? ~ link_newtab ~ space? ~ url) |
        "{{" | "}}" |
        "[[" | "]]" |
        "[[[" | "]]]" |
        "##" |
        "-"{4,} | "~"{4,} |
        "\n"
    ) ~ ANY)+
}

raw = { ("@@@@@@" | "@@" ~ (!("@@" | "\n") ~ ANY)* ~ "@@") }
legacy_raw = { ("``````" | "``" ~ (!("``" | "\n") ~ ANY)* ~ "``") }
// }}}

// Components {{{
hex_digit = @{ '0'..'9' | 'a'..'f' | 'A'..'F' }
space = _{ (" " | "\r" | "\t" | "\n")+ }
integer = @{ "-"? ~ ASCII_DIGIT+ }
file_ident = @{ (!(space | "]]") ~ ANY)+ }
ident = @{ (ASCII_ALPHANUMERIC | "-" | "+" | "_" | "." | "%")+ }
str_inner = @{ (!("\"" | "\\") ~ ANY)* ~ (escape ~ str_inner)? }
string = @{ "\"" ~ str_inner ~ "\"" }
escape = @{ "\\" ~ ("\"" | "\\" | "r" | "n" | "t" | "0" | "'") }
root_url = @{ "/" ~ (!(" " | "\n" | "|" | "]") ~ ANY)+ }
url = @{ (^"http" ~ ^"s"? | ^"ftp") ~ "://" ~ (!(" " | "\n" | "|" | "]") ~ ANY)+ }
email = @{
    (ASCII_ALPHANUMERIC | "-" | "+" | "_" | ".")+ ~ "@" ~
    (ASCII_ALPHANUMERIC | "-")+ ~ "." ~
    (ASCII_ALPHANUMERIC | ".")+
}
COMMENT = _{ "[!--" ~ (!"--]" ~ ANY)* ~ "--]" }
// }}}

// vim: set fdm=marker foldlevel=0:
